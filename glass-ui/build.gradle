plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}
apply from: '../publish.gradle'

def picassoVersion = "2.8"
def fastAdapterVersion = "5.4.1"

def compiler = javaToolchains.compilerFor {
    languageVersion = JavaLanguageVersion.of(11)
}

android {
    compileSdkVersion sdkVersion
    buildToolsVersion buildToolsVersion
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion sdkVersion
        versionCode 1
        versionName moduleVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    // For Kotlin projects
    kotlinOptions {
        jvmTarget = '11'
        jdkHome = compiler.get().metadata.installationPath.asFile.absolutePath
    }

    buildFeatures {
        viewBinding true
    }

    aaptOptions.cruncherEnabled = false
}

dependencies {
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    api "com.squareup.picasso:picasso:$picassoVersion"
    api "com.mikepenz:fastadapter:$fastAdapterVersion"
    api "com.mikepenz:fastadapter-extensions-diff:$fastAdapterVersion"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation project(':core-ui')
}