/*
 * Copyright 2021-2022 Bandyer @ https://www.bandyer.com
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *           http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

buildscript {
    ext.dokka_version = "0.10.1"

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

apply plugin: 'com.android.library'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'kotlin-kapt'

def gitHubUsername = "Bandyer"
def gitHubProjectName = "Bandyer-Android-Design"

// initialize - do not modify this if your project is on github
def siteUrl = "https://github.com/${gitHubUsername}/${gitHubProjectName}"
def gitUrl = siteUrl + '.git'
def bugTrackerUrl = siteUrl + '/issues/'
def projectName = "Android-Design"
// end - do not modify this if your project is on github

def projectDesc = "This library is the Bandyer's sdk design for android video communication."
def projectGroup = "com.bandyer"
def projectOrg = "bandyer"
def projectLicenses = ["Apache-2.0"]
def projectLicenseUrl = "https://github.com/Bandyer/Bandyer-Android-Design/LICENSE.txt"
def projectKeywords = ['bandyer-android-design', 'sdk-design', 'android-design', 'android', 'video', 'conference', 'helpdesk', 'bandyer', 'webrtc', 'call', 'audio']
def developerFullName = "Bandyer Srl"
group = projectGroup
version = "2.2.3"

// Library versions
def fastAdapterVersion = "5.5.1"
def lifecycleVersion = '2.2.0'
def androidX = "1.2.1"
def vectorDrawable = "1.1.0"
def materialVersion = "1.4.0"
def navHostFragment = "2.3.5"
def core_version = "1.5.0"

android {
    compileSdkVersion sdkVersion
    buildToolsVersion buildToolsVersion
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion sdkVersion
        versionCode 1
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
    }

    aaptOptions.cruncherEnabled = false
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    /*** Platform ***/
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "androidx.activity:activity-ktx:1.2.3"
    implementation "androidx.fragment:fragment-ktx:1.3.5"
    implementation("androidx.appcompat:appcompat:1.3.0")
    implementation("androidx.vectordrawable:vectordrawable:$vectorDrawable")
    implementation("androidx.vectordrawable:vectordrawable-animated:$vectorDrawable")
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.core:core-ktx:$core_version"
    api "com.google.android.material:material:$materialVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navHostFragment"

    /*** Testing ***/
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.11.0'

    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'org.mockito:mockito-android:2.+'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.3.0')

    compileOnly 'com.github.imablanco:Zoomy:1ee82834df'

    /*** UI ***/
    api 'com.squareup.picasso:picasso:2.71828'
    api 'androidx.constraintlayout:constraintlayout:2.1.2'
    api "com.mikepenz:fastadapter:$fastAdapterVersion"
    api "com.mikepenz:fastadapter-extensions-diff:$fastAdapterVersion"

    implementation("androidx.recyclerview:recyclerview:$androidX")
    api 'com.google.android.flexbox:flexbox:3.0.0'

    /*** BANDYER ***/
    implementation "com.bandyer:bandyer-android-common:$bandyer_common"
}


Properties properties = new Properties()
if (project.rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

install {
    repositories.mavenInstaller {
        pom.project {
            name projectName
            description projectDesc
            packaging 'aar'
            groupId projectGroup
            version version
            url siteUrl
            licenses {
                license {
                    name projectLicenses[0]
                    url projectLicenseUrl
                }
            }
            developers {
                developer {
                    id properties.getProperty("bintray.user")
                    name developerFullName
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl

            }
        }
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url = "s3://maven-bandyer/releases/"
                credentials(AwsCredentials) {
                    accessKey = properties.getProperty("maven.accessKey")
                    secretKey = properties.getProperty("maven.secretKey")
                }
            }
        }

        publications {
            release(MavenPublication) {
                from components.release
                groupId = projectGroup
                artifactId = project.getName()
                version = version
                artifact kdocJar
                artifact sourcesJar
            }
        }
    }
}

task downloadIcons(type: Exec) {
    workingDir '../scripts'
    commandLine 'python', './download_zeplin_icons.py', properties.getProperty("zeplin.token"), properties.getProperty("zeplin.styleguide"), project.getName()
}

task invalidateCache(type: Exec) {
    workingDir '../scripts'
    def publishPath = "releases"
    def packageName = projectGroup + "." + project.getName()
    commandLine 'python', './invalidate_s3_cache.py', properties.getProperty("maven.accessKey"), properties.getProperty("maven.secretKey"), properties.getProperty("maven.distributionId"), publishPath, packageName, version
}

task publishUpload {
    def dryRun = true
    def publishTask = 'publishToMavenLocal'
    if (!dryRun) publishTask = 'publish'
    dependsOn publishTask
    dependsOn 'invalidateCache'
    tasks.findByName('invalidateCache').mustRunAfter publishTask
}

task dokkaDoc() {
    dependsOn createApiDoc("genKDoc", "html", "kDoc")
}

def createApiDoc(def name, def format, def dir) {
    return tasks.create(name, org.jetbrains.dokka.gradle.DokkaTask) {
        outputFormat = format
        outputDirectory = "$buildDir/$dir"
        configuration {
            reportUndocumented = true
            skipEmptyPackages = true // Do not create index pages for empty packages
        }
    }
}

task kdocJar(type: Jar, dependsOn: dokkaDoc) {
    archiveClassifier.set("javadoc")
    from "$buildDir/kDoc"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives kdocJar
    archives sourcesJar
}